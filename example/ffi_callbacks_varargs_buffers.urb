ffi_bind_all = (path, ...sigs){
  h = ffi.open(path);
  if (h == null) { return null; }
  api = { __lib = h };
  each(sig in sigs) {
    head = sig.split("(")[0].trim();
    parts = head.split(" ");
    name = (parts[parts.size - 1].split("!")[0] + "");
    ptr = ffi.sym(h, name);
    if (ptr == null) { return null; }
    api[name] = ffi.bind(ptr, sig);
  }
  return api;
};

load_fixture = (path){
  return ffi_bind_all(path,
    "i32 sum_variadic_i32(i32, ...)",
    "i32 call_cb_i32(void*, i32, i32)",
    "i32 cstr_len(char*)"
  );
};

lib = load_fixture("./tests/ffi/libffi_view_struct.so");
if (lib == null) { lib = load_fixture("./tests/ffi/libffi_view_struct.dylib"); }
if (lib == null) { lib = load_fixture("./tests/ffi/libffi_view_struct.dll"); }
if (lib == null) {
  println("ffi fixture not found; build tests/ffi/libffi_view_struct first.");
  return 0;
}

println("sum variadic:", lib.sum_variadic_i32(4, 10, 20, 30, 40));

cb = ffi.callback("i32 cb(i32, i32)", (a, b){ return a + b; });
println("callback:", lib.call_cb_i32(cb, 7, 5));

buf = ffi.buffer(5);
arr = ffi.viewArray(buf, "u8", 5);
arr[0] = 79; // 'O'
arr[1] = 75; // 'K'
arr[2] = 0;
println("ffi.string:", ffi.string(buf));
println("cstr_len:", lib.cstr_len(buf));
ffi.free(buf);

schema = { a = "const int32", b = "int32" };
p = ffi.new(schema);
v = ffi.view(p, schema);
v.a = 99; // ignored (non-strict warning)
v.b = 11;
println("const view:", v.a, v.b);
ffi.free(p);
