ffi_sig_name = (sig){
  s = sig.trim(),
  depth = 0,
  open = -1,
  i = s.size - 1,
  while (i >= 0) {
    ch = s.slice(i, i + 1),
    if (ch == ")") {
      depth++,
    } else if (ch == "(") {
      depth--,
      if (depth == 0) {
        open = i,
        break,
      }
    }
    i--,
  }
  if (open < 0) { return null, }
  head = s.slice(0, open).trim(),
  parts = head.split(" "),
  if (parts.size == 0) { return null, }
  return (parts[parts.size - 1].split("!")[0] + ""),
},

ffi_bind_all = (path, ...sigs){
  h = ffi.open(path),
  if (h == null) { return null, }
  api = { __lib = h },
  each(sig in sigs) {
    name = ffi_sig_name(sig),
    ptr = ffi.sym(h, name),
    if (ptr == null) { return null, }
    api[name] = ffi.bind(ptr, sig),
  }
  return api,
},

// ffi_printf.urb
// Example of variadic printf via FFI (ffi.open + ffi.sym + ffi.bind)
// Requires ENABLE_FFI=1 and ENABLE_FFI_CALLS=1.

load_libc = (path){
  return ffi_bind_all(path,
    "i32 printf(char*, ...)"
  ),
},

lib = load_libc("libc.so.6"),
if (lib == null) { lib = load_libc("libSystem.B.dylib"), }
if (lib == null) { lib = load_libc("msvcrt.dll"), }
if (lib == null) {
  println("libc not found"),
  return 0,
}

n1 = lib.printf("hello from disturb + printf"),
n2 = lib.printf(" | int=%d float=%.2f str=%s", 42, 3.14159, "ok"),

println("printf return bytes:", n1, n2),
