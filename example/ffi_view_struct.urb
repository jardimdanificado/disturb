ffi_sig_name = (sig){
  s = sig.trim(),
  depth = 0,
  open = -1,
  i = s.size - 1,
  while (i >= 0) {
    ch = s.slice(i, i + 1),
    if (ch == ")") {
      depth++,
    } else if (ch == "(") {
      depth--,
      if (depth == 0) {
        open = i,
        break,
      }
    }
    i--,
  }
  if (open < 0) { return null, }
  head = s.slice(0, open).trim(),
  parts = head.split(" "),
  if (parts.size == 0) { return null, }
  return (parts[parts.size - 1].split("!")[0] + ""),
},

ffi_bind_all = (path, ...sigs){
  h = ffi.open(path),
  if (h == null) { return null, }
  api = { __lib = h },
  each(sig in sigs) {
    name = ffi_sig_name(sig),
    ptr = ffi.sym(h, name),
    if (ptr == null) { return null, }
    api[name] = ffi.bind(ptr, sig),
  }
  return api,
},

inner = {
  a = "int8",
  b = "int16"
},

outer = {
  a = "int32",
  b = "float64",
  d = "struct(inner)"
},

load_fixture = (path){
  return ffi_bind_all(path,
    "pointer(outer) make_outer()",
    "void free_outer(pointer(outer))",
    "i32 outer_sizeof()",
    "i32 outer_off_d()",
    "i32 inner_off_b()",
    "i32 outer_get_a(pointer(outer))",
    "i32 inner_get_b(pointer(outer))"
  ),
},

lib = load_fixture("./tests/ffi/libffi_view_struct.so"),
if (lib == null) { lib = load_fixture("./tests/ffi/libffi_view_struct.dylib"), }
if (lib == null) { lib = load_fixture("./tests/ffi/libffi_view_struct.dll"), }
if (lib == null) {
  println("ffi test library not available"),
  return 0,
}

schema = outer,

layout = memory.compile(schema),
println("sizeof: " + memory.sizeof(layout).string),
println("offsetof(d.b): " + memory.offsetof(layout, "d.b").string),

ptr = lib.make_outer(),
v = memory.view(ptr, layout),

v.a = 45,
v.d.b = 45,

println("outer.a: " + lib.outer_get_a(ptr).string),
println("inner.b: " + lib.inner_get_b(ptr).string),

lib.free_outer(ptr),
