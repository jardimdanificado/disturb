// 14_ffi_struct_views_bind.urb
// Advanced FFI: struct views + function pointer bind.
// Requires ENABLE_FFI=1 and tests/ffi/libffi_view_struct.(so|dylib|dll) built.

load_fixture = (path){
  return ffi.load(path,
    "pointer<outer> make_outer()",
    "void free_outer(pointer<outer>)",
    "i32 outer_get_a(pointer<outer>)",
    "i32 inner_get_b(pointer<outer>)",
    "void* get_add_i32_ptr()",
    "i32 sum_outer_value(struct<outer>)",
    "struct<outer> make_outer_value(i32, f64, i8, i16)"
  );
};

inner = { a = "int8", b = "int16" };
schema = {
  a = "int32",
  b = "float64",
  d = "struct<inner>"
};
outer = schema;

lib = load_fixture("./tests/ffi/libffi_view_struct.so");
if (lib == null) { lib = load_fixture("./tests/ffi/libffi_view_struct.dylib"); }
if (lib == null) { lib = load_fixture("./tests/ffi/libffi_view_struct.dll"); }

if (lib == null) {
  println("ffi test library not available");
  return 0;
}

ptr = lib.make_outer();
v = ffi.view(ptr, schema);
v.a = 45;
v.d.b = 123;

println(v.a);
println(v.d.b);
println(lib.outer_get_a(ptr));
println(lib.inner_get_b(ptr));
println(lib.sum_outer_value(v));
lib.free_outer(ptr);

add_ptr = lib.get_add_i32_ptr();
add = ffi.bind(add_ptr, "i32 add(i32, i32)");
println(add(20, 22));

ret = lib.make_outer_value(7, 1.5, 2, 9);
println(ret.a);
println(ret.d.b);
