ffi_sig_name = (sig){
  s = sig.trim(),
  depth = 0,
  open = -1,
  i = s.size - 1,
  while (i >= 0) {
    ch = s.slice(i, i + 1),
    if (ch == ")") {
      depth++,
    } else if (ch == "(") {
      depth--,
      if (depth == 0) {
        open = i,
        break,
      }
    }
    i--,
  }
  if (open < 0) { return null, }
  head = s.slice(0, open).trim(),
  parts = head.split(" "),
  if (parts.size == 0) { return null, }
  return (parts[parts.size - 1].split("!")[0] + ""),
},

ffi_bind_all = (path, ...sigs){
  h = ffi.open(path),
  if (h == null) { return null, }
  api = { __lib = h },
  each(sig in sigs) {
    name = ffi_sig_name(sig),
    ptr = ffi.sym(h, name),
    if (ptr == null) { return null, }
    api[name] = ffi.bind(ptr, sig),
  }
  return api,
},

// FFI callbacks, variadic signatures, and raw buffer/string helpers.
// Requires ENABLE_FFI=1 and tests/ffi/libffi_view_struct.(so|dylib|dll) built.

load_fixture = (path){
  return ffi_bind_all(path,
    "i32 sum_variadic_i32(i32, ...)",
    "i32 call_cb_i32(void*, i32, i32)",
    "i32 cstr_len(char*)"
  ),
},

lib = load_fixture("./tests/ffi/libffi_view_struct.so"),
if (lib == null) { lib = load_fixture("./tests/ffi/libffi_view_struct.dylib"), }
if (lib == null) { lib = load_fixture("./tests/ffi/libffi_view_struct.dll"), }
if (lib == null) {
  println("ffi fixture not found, build tests/ffi/libffi_view_struct first."),
  return 0,
}

// Variadic call: `...` in signature.
println(lib.sum_variadic_i32(5, 1, 2, 3, 4, 5)), // 15

// Lambda callback exported as C function pointer.
adder = ffi.callback("i32 cb(i32, i32)", (a, b){ return a + b, }),
println(lib.call_cb_i32(adder, 20, 22)), // 42

// Owned raw buffer + byte view + C string round-trip.
buf = ffi.buffer(8),
bytes = ffi.viewArray(buf, "u8", 8),
bytes[0] = 72, // H
bytes[1] = 105, // i
bytes[2] = 0,
println(ffi.string(buf)),     // "Hi"
println(lib.cstr_len(buf)),   // 2
ffi.free(buf),

// Qualifiers in schema strings.
s = { a = "const int32", b = "volatile int32", c = "restrict int32" },
p = ffi.new(s),
v = ffi.view(p, s),
v.a = 123, // ignored in non-strict mode
v.b = 7,
v.c = 9,
println(v.a, v.b, v.c), // 0 7 9
ffi.free(p),
