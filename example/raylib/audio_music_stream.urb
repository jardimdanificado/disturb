// raylib [audio] example - music stream (Disturb port)

raylib = import("lib/raylib.urb"),
rl = raylib.load(),
if (rl == null) {
  println("raylib library not found"),
  gc.collect(),
  return 0,
}

gc.rate = 100,

screenWidth = 800,
screenHeight = 450,

rl.InitWindow(screenWidth, screenHeight, "raylib [audio] example - music stream"),
rl.SetTargetFPS(30),

if (!rl.IsWindowReady()) {
  println("raylib backend not available (DISPLAY missing?)"),
  gc.collect(),
  return 0,
}

audioEnabled = 0,
music = null,
pause = 0,
timePlayed = 0.0,
pan = 0.0,
volume = 0.8,

rl.InitAudioDevice(),
if (rl.IsAudioDeviceReady()) {
  audioEnabled = 1,
  music = rl.LoadMusicStream("../raylib/examples/audio/resources/country.mp3"),
  rl.PlayMusicStream(music),
  rl.SetMusicPan(music, pan),
  rl.SetMusicVolume(music, volume),
}

while (!rl.WindowShouldClose()) {
  if (audioEnabled) {
    rl.UpdateMusicStream(music),

    if (rl.IsKeyPressed(rl.KEY_SPACE)) {
      rl.StopMusicStream(music),
      rl.PlayMusicStream(music),
    }

    if (rl.IsKeyPressed(rl.KEY_P)) {
      pause = !pause,
      if (pause) { rl.PauseMusicStream(music), }
      else { rl.ResumeMusicStream(music), }
    }

    if (rl.IsKeyDown(rl.KEY_LEFT)) {
      pan = pan - 0.05,
      if (pan < -1.0) { pan = -1.0, }
      rl.SetMusicPan(music, pan),
    } else if (rl.IsKeyDown(rl.KEY_RIGHT)) {
      pan = pan + 0.05,
      if (pan > 1.0) { pan = 1.0, }
      rl.SetMusicPan(music, pan),
    }

    if (rl.IsKeyDown(rl.KEY_DOWN)) {
      volume = volume - 0.05,
      if (volume < 0.0) { volume = 0.0, }
      rl.SetMusicVolume(music, volume),
    } else if (rl.IsKeyDown(rl.KEY_UP)) {
      volume = volume + 0.05,
      if (volume > 1.0) { volume = 1.0, }
      rl.SetMusicVolume(music, volume),
    }

    len = rl.GetMusicTimeLength(music),
    if (len > 0.0) { timePlayed = rl.GetMusicTimePlayed(music)/len, }
    else { timePlayed = 0.0, }
    if (timePlayed > 1.0) { timePlayed = 1.0, }
  }

  rl.BeginDrawing(),
  rl.ClearBackground(rl.RAYWHITE.view),

  rl.DrawText("MUSIC SHOULD BE PLAYING!", 255, 150, 20, rl.LIGHTGRAY.view),
  rl.DrawText("LEFT-RIGHT for PAN CONTROL", 320, 74, 10, rl.DARKBLUE.view),
  rl.DrawRectangle(300, 100, 200, 12, rl.LIGHTGRAY.view),
  rl.DrawRectangleLines(300, 100, 200, 12, rl.GRAY.view),
  rl.DrawRectangle(300 + ((pan + 1.0)/2.0)*200 - 5, 92, 10, 28, rl.DARKGRAY.view),

  rl.DrawRectangle(200, 200, 400, 12, rl.LIGHTGRAY.view),
  rl.DrawRectangle(200, 200, timePlayed*400.0, 12, rl.MAROON.view),
  rl.DrawRectangleLines(200, 200, 400, 12, rl.GRAY.view),

  rl.DrawText("PRESS SPACE TO RESTART MUSIC", 215, 250, 20, rl.LIGHTGRAY.view),
  rl.DrawText("PRESS P TO PAUSE/RESUME MUSIC", 208, 280, 20, rl.LIGHTGRAY.view),

  rl.DrawText("UP-DOWN for VOLUME CONTROL", 320, 334, 10, rl.DARKGREEN.view),
  rl.DrawRectangle(300, 360, 200, 12, rl.LIGHTGRAY.view),
  rl.DrawRectangleLines(300, 360, 200, 12, rl.GRAY.view),
  rl.DrawRectangle(300 + volume*200 - 5, 352, 10, 28, rl.DARKGRAY.view),
  if (!audioEnabled) { rl.DrawText("Audio device not available on this system", 200, 410, 20, rl.MAROON.view), }

  rl.EndDrawing(),
}

if (audioEnabled) {
  rl.UnloadMusicStream(music),
  rl.CloseAudioDevice(),
}
rl.CloseWindow(),
gc.collect(),
return 0,
