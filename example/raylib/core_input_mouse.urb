// raylib [core] example - input mouse (Disturb port)

raylib = import("lib/raylib.urb"),
rl = raylib.load(),
if (rl == null) {
  println("raylib library not found"),
  gc.collect(),
  return 0,
}

gc.rate = 100,

screenWidth = 800,
screenHeight = 450,

rl.InitWindow(screenWidth, screenHeight, "raylib [core] example - input mouse"),
rl.SetTargetFPS(60),

if (!rl.IsWindowReady()) {
  println("raylib backend not available (DISPLAY missing?)"),
  gc.collect(),
  return 0,
}

ballColor = rl.DARKBLUE,

while (!rl.WindowShouldClose()) {
  if (rl.IsKeyPressed(rl.KEY_H)) {
    if (rl.IsCursorHidden()) { rl.ShowCursor(), }
    else { rl.HideCursor(), }
  }

  ballPosition = rl.GetMousePosition(),

  if (rl.IsMouseButtonPressed(rl.MOUSE_BUTTON_LEFT)) { ballColor = rl.MAROON, }
  else if (rl.IsMouseButtonPressed(rl.MOUSE_BUTTON_MIDDLE)) { ballColor = rl.LIME, }
  else if (rl.IsMouseButtonPressed(rl.MOUSE_BUTTON_RIGHT)) { ballColor = rl.DARKBLUE, }
  else if (rl.IsMouseButtonPressed(rl.MOUSE_BUTTON_SIDE)) { ballColor = rl.PURPLE, }
  else if (rl.IsMouseButtonPressed(rl.MOUSE_BUTTON_EXTRA)) { ballColor = rl.YELLOW, }
  else if (rl.IsMouseButtonPressed(rl.MOUSE_BUTTON_FORWARD)) { ballColor = rl.ORANGE, }
  else if (rl.IsMouseButtonPressed(rl.MOUSE_BUTTON_BACK)) { ballColor = rl.BEIGE, }

  rl.BeginDrawing(),
  rl.ClearBackground(rl.RAYWHITE.view),
  rl.DrawCircleV(ballPosition, 40.0, ballColor.view),
  rl.DrawText("move ball with mouse and click mouse button to change color", 10, 10, 20, rl.DARKGRAY.view),
  rl.DrawText("Press 'H' to toggle cursor visibility", 10, 30, 20, rl.DARKGRAY.view),

  if (rl.IsCursorHidden()) { rl.DrawText("CURSOR HIDDEN", 20, 60, 20, rl.RED.view), }
  else { rl.DrawText("CURSOR VISIBLE", 20, 60, 20, rl.LIME.view), }

  rl.EndDrawing(),
}

rl.CloseWindow(),
gc.collect(),
return 0,
